---
arturl_encode: "68747470733a2f2f626c6f672e:6373646e2e6e65742f6875616e673230303833323030303536:2f61727469636c652f64657461696c732f3231303239373831"
layout: post
title: "51单片机开发系列一-51单片机开发环境搭建以及入门汇编代码"
date: 2025-01-08 00:10:31 +0800
description: "51单片机开发系列一51单片机开发环境搭建以及入门汇编代码象棋小子    10482729751. "
keywords: "单片机编程架构规范"
categories: ['51单片机开发']
tags: ['汇编', '嵌入式开发', '单片机', 'Stc', 'Keil']
artid: "21029781"
image:
    path: https://api.vvhan.com/api/bing?rand=sj&artid=21029781
    alt: "51单片机开发系列一-51单片机开发环境搭建以及入门汇编代码"
render_with_liquid: false
featuredImage: https://bing.ee123.net/img/rand?artid=21029781
featuredImagePreview: https://bing.ee123.net/img/rand?artid=21029781
---

# 51单片机开发系列一-51单片机开发环境搭建以及入门汇编代码

## 51单片机开发系列一

### 51单片机开发环境搭建以及入门汇编代码

象棋小子    1048272975

#### 1. 51单片机概述

51单片机是对所有兼容Intel 8031指令系统的单片机的统称。目前教科书基本都是以早期的MCS-51为原型，讲解微机的原理及其接口技术的。早期的51单片机功能都较弱，需扩展rom，ram等才能组成一个较复杂的系统。而现今流行的8位51单片机都比早期芯片作了较多的功能扩展，性能强劲。生产51单片机的厂家有很多，像atmel的at80c51系列，华邦w78c051系列，宏晶stc80c51系列等，其中台湾宏晶stc系列51单片机在国内尤为流行。因此，笔者采用宏晶性价比较高的一款51单片机stc12c5a60s2来作开发讲解。该款51单片机已经是宏昌第N代产品了，其主要特征如下：

1.1. 增强型8051 CPU，1T，单时钟/机器周期，指令代码完全兼容传统8051;

1.2. 工作频率范围：0 - 35MHz，相当于普通8051的 0～420MHz;

1.3. 用户应用程序空间60K字节，片上集成1280字节RAM;

1.4. 通用I/O口36个(以封装PDIP40为例)，可设置成准双向口/弱上拉，推挽/强上拉，仅为输入/高阻，开漏，每个I/O口驱动能力均可达到20mA，但整个芯片最大不要超过55Ma;

1.5. ISP(在系统可编程)/IAP(在应用可编程)，无需专用编程器，无需专用仿真器 可通过串口(P3.0/P3.1)直接下载用户程序。

1.6. 内部集成MAX810专用复位电路,2路PWM,8路高速10位A/D转换(250K/S)，EEPROM，看门狗，内部RC振荡器，4个定时器，7路外部I/O口中断等。

尤其需注意的是stc新一代的单片机都是1T单时钟/机器周期，与课本介绍的早期51单片机是12T单周期是不一样的，软件实现延时时需作注意，其余扩充的特殊功能寄存器等请参考stc12c5a60s2的数据手册。

#### 2. 51单片机编程环境概述

##### 2.1. 代码编译工具

51单片机开发软件基本无疑选用Keil C51集成开发环境。Keil C51是德国Keil Software公司(ARM公司收购了)出品的51系列兼容单片机C语言软件开发系统，提供了包括C编译器、宏汇编、连接器、库管理和一个功能强大的仿真调试器等，目前最新版本已经到了uVision5。这里需要说明的是，Keil C51并不完全与ANSI C一样，Keil C51由于面向的是51单片机，为了更好地详述51的架构，Keil C51扩展了一些独特的关键字，语法描述等。如用code说明为常量放在代码区，data说明直接寻址区的变化，sfr声明特殊功能寄存器等。

![](https://img-blog.csdn.net/20140311182924515?watermark/2/text/aHR0cDovL2Jsb2cuY3Nkbi5uZXQvaHVhbmcyMDA4MzIwMDA1Ng==/font/5a6L5L2T/fontsize/400/fill/I0JBQkFCMA==/dissolve/70/gravity/Center)

图2.1-1

##### 2.2. 代码调试工具

###### 2.2.1. Keil自带仿真调试器

Keil集成开发环境除了编译链接工具外，还自带了一个功能强大的仿真调试器。当然软仿真是能够看到Keil编译后的汇编代码，单步调试可以跟踪各个寄存器的状态变化，但是软仿真是无法得到真实的外部输入状态的，如仿真真实开发板的按键输入等。代码调试时往往需要知道编译器是否按照要求进行代码的编译处理，因此，可以让编译器输出它是如何编译，链接文件的，我们可以查看这些了解编译器编译c生成的汇编代码，链接的符号，内存分配之类的信息。Keil在Target属性中Listing列可进行设置，如C Compiler Listing选项中是c编译器输出选项，点上Assembly Code即输出c编译对应的汇编代码，在文件.lst中。C Preprocessor Listing为c编译器预处理输出的信息，AssemblerListing为汇编器输出的处理信息，LinkerListing为链接器输出的处理信息(在.m51后缀文件中)，这包括编译器对内存的分配，各个函数符号等。通常编译后的汇编代码以及代码的链接信息是可以跟踪查看，以判断代码的问题所在。

![](https://img-blog.csdn.net/20140311183050500?watermark/2/text/aHR0cDovL2Jsb2cuY3Nkbi5uZXQvaHVhbmcyMDA4MzIwMDA1Ng==/font/5a6L5L2T/fontsize/400/fill/I0JBQkFCMA==/dissolve/70/gravity/Center)

图2.2.1-1

![](https://img-blog.csdn.net/20140311183253406?watermark/2/text/aHR0cDovL2Jsb2cuY3Nkbi5uZXQvaHVhbmcyMDA4MzIwMDA1Ng==/font/5a6L5L2T/fontsize/400/fill/I0JBQkFCMA==/dissolve/70/gravity/Center)

图2.2.1-2

###### 2.2.2. Proteus仿真软件

Proteus软件是英国Lab Center Electronics公司出版的EDA工具软件，它不仅具有其它EDA工具软件的仿真功能，还能仿真单片机及外围器件。Proteus具有电路仿真功能，能仿真一些最基本电子元器件，如led，数码管，键盘等，并且是能仿真51单片机代码运行的。在这里需要说明的是，请务必不能以仿真电路的效果图与真实的硬件开发混搅，电路仿真软件往往都是逻辑上的电路连接，完全不能够用来说明真实硬件开发过程。此外，Proteus仿真51单片机也会有一些逻辑的问题，例如，在硬件上真实可运行的代码在Proteus上无法仿真达到效果，修改一下代码顺序即可。总之，对于小代码，Proteus都是可以胜任的，如果代码太复杂，仿真任务会达到100%，无法再进行仿真了。因此，笔者推荐对于初学者，可先用Proteus进行仿真，进行一些最基本的练习，熟悉汇编语言，51单片机状态变化，c语言等，验证自己在51单片机架构学习中的一些理解，代码实现的理解。学习到一定的程度还是需要一块51开发板进行开发练习的，毕竟仿真软件只能仿真很少部分的外围，也不能仿真复杂的硬件及代码。

![](https://img-blog.csdn.net/20140311183337875?watermark/2/text/aHR0cDovL2Jsb2cuY3Nkbi5uZXQvaHVhbmcyMDA4MzIwMDA1Ng==/font/5a6L5L2T/fontsize/400/fill/I0JBQkFCMA==/dissolve/70/gravity/Center)

图2.2.2-1

##### 2.3. 代码烧录工具

Keil C51编译生成的hex文件通过STC\_ISP工具，连接串口线(一般采用usb转串口线)进行下载。代码烧写传输是通过51单片机的uart串口信号线Tx，Rx完成的，stc单片机实现isp下载是因为芯片内部有厂商的固化代码，上电复位后是先执行固化代码，检测串口有无接收到特定的命令，如果有则进入下载模式，与上位机的isp下载软件进行通信，从而把代码下载进单片机rom区。如果没有有效的串口下载命令，则跳转执行真正的用户代码，即从0000H处开始执行代码。

![](https://img-blog.csdn.net/20140311183446562?watermark/2/text/aHR0cDovL2Jsb2cuY3Nkbi5uZXQvaHVhbmcyMDA4MzIwMDA1Ng==/font/5a6L5L2T/fontsize/400/fill/I0JBQkFCMA==/dissolve/70/gravity/Center)

图2.3-1

#### 3. 汇编流水灯入门代码

笔者认为学习51单片机并不是能通过别人的例子用c语言模仿写出类似的功能即可，必须要对自己的编码意图比较清晰，这样脱离任何例程都是可以自己掌控编写代码。因此学习51单片机其实更准确来说是学习微机的原理以及接口技术。而微机的原理以及接口技术对于51，arm或其它架构的mcu都是通用的，通过51来学习微机原理会涉及到汇编语言，因为只有汇编语言才能直接描述51内部的工作状态。笔者以过来人的身份推荐初学者从51微机原理，汇编学起。C语言只是简化封装了汇编语言的一些处理过程，学完汇编，c语言也自然会达到相应的水平。此外，对于软件出错调试，只能跟踪汇编代码，查看寄存器的状态判断，而想学习arm，从事更深入的嵌入式开发，汇编是必不可少的。

##### 3.1. 硬件原理图

8个LED连接到P0口，当短接CON2后，只要P0口对应位为0(低电平)，相应的LED则被点亮。此外说明一下为什么不用P0对应位为1时点亮而用0，因为传统51单片机I/O口是弱上拉的，高电平是输不出大电流的(相对低电平)，高电平拉电流估计是ua级，但低电平灌电流几个ma是不成问题的。对于stc系列51单片机，I/O口是可以配置成推挽输出的，这样高低电平都是可以达到20ma(手册数据)的输出/吸收电流。

![](https://img-blog.csdn.net/20140311183542937?watermark/2/text/aHR0cDovL2Jsb2cuY3Nkbi5uZXQvaHVhbmcyMDA4MzIwMDA1Ng==/font/5a6L5L2T/fontsize/400/fill/I0JBQkFCMA==/dissolve/70/gravity/Center)

图3.1-1

##### 3.2. 工程搭建

打开Keil C51，Project->NewuVersion Project，保存项目后，选择cpu为Atmel的AT89C52的51单片机，这里需要说明的是，Keil没有stc系列的51单片机选择，只要是51内核，在Keil下可选择任一厂家，任一款51单片机进行代码编写，因为代码都是兼容的。而不同厂商芯片之间的差异只是rom大小，ram大小，片内外设以及一些厂家特有的特殊功能寄存器的定义。这些都可以在工程中，代码中重新定义，编译器会老老实实按照要求编译代码。选择了cpu后，会提示是否加入51的启动代码到工程中，由于我们编写的是汇编语言，此处不需要，加入后启动代码会与我们自己的汇编代码定义冲突。这里需要说明的是，启动代码是初始化c环境需要的文件，启动代码会设置c代码运行时的堆栈，清零全局变量，静态变量区等。这就是为什么我们在c文件中定义一个全局变量，默认这个变量的初始值为0(C标准)。

##### 3.3. 代码编写

创建一个新文件，命名为LEDs.ASM，ASM为51汇编文件后缀，保存并加入工程。汇编的一些基本用法在代码注释中有说明，更多的汇编用法请google，百度。这里需要说明的是，51单片机第一条指令位置是在0H，后面相邻的地址是分配给相应的中断进入的，因此第一条指令往往会跳转避开中断向量地址区。以下代码实现8个LED灯轮流点亮，点亮延时1s，这个汇编代码是模仿c语言函数结构化编程的，里面可以类似认识到c编译器大概是如何处理c函数并生成汇编的，当然编译器汇编质量基本是无法达到人工汇编质量的。

ORG 0H        ; 表示后面紧跟的那条指令的地址是 0000H

JMP     Begin ; 无条件跳转到Begin处,以避免中断向量地址

ORG 0BH      ;000BH处为定时器T0的中断处理入口

JMP T0\_INT ; 未使用T0定时器中断,只供代码说明

T0\_INT:

; 中断发生时会自动把当前程序运行地址PC压入栈sp

; 中断处理完后用RETI中断返回,从栈sp中出栈到PC返回打断程序处

RETI

LED1 EQUP0.0  ; LED1由P0口第0位控制，以下类似

LED2 EQU P0.1

LED3 EQU P0.2

LED4 EQU P0.3

LED5 EQU P0.4

LED6 EQU P0.5

LED7 EQU P0.6

LED8 EQU P0.7

ORG 100H

Begin:

MOV P0, #0xff ;P0口输出全1，所有LED灭

LOOP:

; R6，R7为调用函数的参数传入，参数为16位，需2字节

; \_Delay\_ms对应c函数原型为void Delay\_ms(intnCount)

; 共延时nCount \* 1ms(12M普通8051)，对于stc指令周期1T的

; 延时nCount \* (1/6)ms (12M)

CLR LED1 ;直接位清0指令，清除P0口第0位，LED1亮

MOV R7, #(1000& 0xff) ; 参数为1000，普通8051延时1s

MOV    R6, #((1000>>8) & 0xff)   ; 16位变量需用2字节

CALL \_Delay\_ms ;延时n个1ms(普通51)，延时n个1/6ms(stc 51)

SETB LED1 ; 直接位位置位指令，置位P0口第0位，LED1灭

CLR     LED2

MOV R7, #(1000& 0xff) ; 普通8051延时1s，stc应改1000为1000\*6

MOV    R6, #((1000>>8) & 0xff) ; 能让编译器运算的不要自己手动计算

CALL \_Delay\_ms

SETB LED2

CLR     LED3

MOV R7, #(1000& 0xff)

MOV    R6, #((1000>>8) & 0xff)

CALL \_Delay\_ms

SETB LED3

CLR     LED4

MOV R7, #(1000& 0xff)

MOV    R6, #((1000>>8) & 0xff)

CALL \_Delay\_ms

SETB LED4

CLR     LED5

MOV R7, #(1000& 0xff)

MOV    R6, #((1000>>8) & 0xff)

CALL \_Delay\_ms

SETB LED5

CLR     LED6

MOV R7, #(1000& 0xff)

MOV    R6, #((1000>>8) & 0xff)

CALL \_Delay\_ms

SETB LED6

CLR     LED7

MOV R7, #(1000& 0xff)

MOV    R6, #((1000>>8) & 0xff)

CALL \_Delay\_ms

SETB LED7

CLR     LED8

MOV R7, #(1000& 0xff)

MOV    R6, #((1000>>8) & 0xff)

CALL \_Delay\_ms

SETB LED8

MOV P0, #0    ; 常数存入直接地址，清零P0口，LED全亮

MOV R7, #(1000& 0xff)

MOV    R6, #((1000>>8) & 0xff)

CALL \_Delay\_ms

MOV P0, #0xff

MOV R7, #(1000& 0xff)

MOV    R6, #((1000>>8) & 0xff)

CALL \_Delay\_ms

JMP     LOOP ; LOOP循环

; 按照keil c与汇编调用规则命令函数及传参，可先不管

; 用CALL调用函数会硬件把调用处PC地址压栈

; 处理完后用RET函数返回,从栈sp中出栈到PC返回调用程序处

\_Delay\_ms:

PUSH ACC ; 子函数需用到累加器，需压栈保存以免覆盖调用前值

PUSH PSW ; 用到程序状态寄存器，需压栈

MOV    A, R0 ; 用到R0寄存器，没有直接寄存器名压栈指令

PUSH ACC  ; 通过累加器完成压栈

MOV A, R1 ; 用到R1寄存器，同理压栈

PUSH ACC

; 以下是16位的递减1减法运算，高8位在R6中，低8位在R7中

; 数据运算涉及到进位/借位，只能通过累加器ACC来完成

Delay:

CLR C ; 清除借位标志

MOV A, R7 ; 低8位值给到累加器，只有针对累加器运算的指令

SUBB A, #1 ; 自减1，会改变程序状态标志(进位/借位)

MOV R7, A ; 运算结果返回到原变量中

JNC DelayOnce ; 没有借位说明延时次数未到，跳转延时一次

CLR C ; 产生了借位，需向高8位R6减1

MOV A, R6

SUBB A, #1

MOV R6, A

JNC DelayOnce ; 高8位未减至0，说明延时次数未到

POP ACC   ; 高8位也为0，不能再给低8位借位了，延时到返回

MOV R1, A ; 返回时先出栈，出栈顺序与入栈顺序相反

POP ACC     ; 并且PUSH与POP指令必须一一对应，不然只有让程序飞

MOV R0, A

POP PSW

POP ACC

RET ; 子函数返回，与c函数是一至的

; DelayOnce执行一次机器周期总数为 1+R0\*(1+R1\*2+2)+2=997个

; 若普通51晶振12M，每个机器周期1us，则DelayOnce一次延时1ms

; 对于stc51，同等晶振下，指令速度快了6倍，DelayOnce延时1/6ms

DelayOnce:

MOV R0, #2 ;普通51机器周期数1(stc这条指令比普通的快6倍)

Delay1:

MOV R1, #247 ;普通51机器周期数1(stc快6倍)

DJNZ R1, $ ; R7减1不为0则跳转到当前地址循环，机器周期数2

DJNZ R0, Delay1; 机器周期数2(stc快6倍)

JMP Delay ; 已延时一次，机器周期数2(stc快6倍)

END            ; 汇编代码结束

##### 3.4. 代码运行

在Keil上选中Create HEX File复选框，编译生成hex文件，可以直接在Keil进行debug，通过查看P0口数据的变化以跟踪代码等，注意设置仿真的时钟为12M。更直观的是用Proteus搭建一个51单片机仿真电路，在P0口连接8个LED，即可看到效果，注意设置仿真的时钟为12M。如果有51开发板，把代码下载进单片机中即可(但对于stc 1T 51单片机需修改一下代码中延时的参数)。

![](https://img-blog.csdn.net/20140311183641046?watermark/2/text/aHR0cDovL2Jsb2cuY3Nkbi5uZXQvaHVhbmcyMDA4MzIwMDA1Ng==/font/5a6L5L2T/fontsize/400/fill/I0JBQkFCMA==/dissolve/70/gravity/Center)

图3.4-1 Proteus仿真代码效果图

#### 4. 小结

笔者概述性介绍了51单片机(以stc12c5a60s2为例)，讲解了其基本的编译，调试工具、环境的搭建。简单给出了采用c函数结构编程的流水灯汇编代码，让读者对汇编，c编译汇编过程有一个初步的认识，由于笔者的认识有限，文章中个人观点有些可能非常片面，以及文章中可能存在不少的错误，恳请大家指正。

由于一门技术是不可能用只言片语就能说清的，笔者也只能在文章中概述性讲述，可能会有初学者觉得例程汇编过难，笔者想说明的是，学习是一个渐近的过程，只要学习了，那么就会有潜移默化的进步。以下资料笔者认为跟本文学习是相关的，推荐大家学习或参考。

a. stc12c5a60s2数据手册，非常有用，里面有很多编程示例代码以及详尽的stc51系列单片机寄存器编程描述。

b. 单片机初学者实验指导书.doc，只对入门者，讲述怎样连接usb转串口线下载代码，keil安装以及工程搭建。

c. Keil软件使用手册.ppt，简单讲解了Keil软件工程的搭建，调试介绍

d. Proteus中文入门教程.doc，讲述了Proteus如何搭建电路以及进行51单片机的仿真

e. 51汇编指令.ppt，较好地介绍了51汇编指令，伪指令等的使用，但细节不够，如指令执行后栈变化没有说明，以51微机原理教科书汇编指令资料为最佳。

f. LEDs.ASM，汇编工程代码，加入到keil工程即可编译。

<http://pan.baidu.com/s/1nbXwU>